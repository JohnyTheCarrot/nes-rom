.include "constants.incl"

.importzp nmi_ready

.segment "OAM"
oam: .res 256

.BSS
palette: .res 32

.CODE

.proc vblank
	; save registers
	pha
	txa
	pha
	tya
	pha

	; Check whether we're ready to render, skip to end if not
	lda nmi_ready
	bne :+
	jmp end
:

	cmp #NMI_SIG_RENDERING_OFF 	; Command to turn off next V-Blank
	bne continue 			; If nmi_ready != 2, we can render.

	; nmi_ready == 2, clear PPU_MASK
	lda #0
	sta PPU_MASK
	ldx #NMI_SIG_CLEAR
	stx nmi_ready
	jmp end

continue:
	ldx #0
	stx PPU_SPRRAM_ADDRESS
	lda #>oam
	sta SPRITE_DMA

	; Set PPU address to $3F00. This process is a bit strange,
	; you write the upper and lower byte to this same pointer sequentially.
	lda #>PPU_ADDR_PALETTE
	sta PPU_VRAM_ADDRESS2
	stx PPU_VRAM_ADDRESS2

	lda PPU_STATUS
	lda #%10011000 
	sta PPU_CONTROL

	; Reset scroll
	lda #$00
	sta PPU_VRAM_ADDRESS1
	sta PPU_VRAM_ADDRESS1

	ldx #0 		; Using the X register as the loop index.
transfer_loop:
	lda palette, x
	sta PPU_VRAM_IO
	inx
	cpx #32		; sizeof(palette)
	bcc transfer_loop

	; Enable rendering
	lda #%00011110
	sta PPU_MASK

	ldx #NMI_SIG_CLEAR
	stx nmi_ready

end:
	; pop registers
	pla
	tay
	pla
	tax
	pla
	rti
.endproc

.export vblank, oam, palette
